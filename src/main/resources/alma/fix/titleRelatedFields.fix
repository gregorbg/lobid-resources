# 245 - Title Statement (NR) - Subfield: $a (NR), $b (NR), $n (R), $p (R)

copy_field("245??.a","@title")
replace_all("@title", "^[©]|\\s?[,.:;/=]?$", "")
replace_all("@title", "<", "")
replace_all("@title", ">", "")


set_array("@titleOfSubSeries_n")
do list(path:"245??.n", "var":"$i")
  copy_field("$i","@titleOfSubSeries_n.$append")
end
join_field("@titleOfSubSeries_n", ". ")

set_array("@titleOfSubSeries_p")

do list(path:"245??.p", "var":"$i")
  copy_field("$i","@titleOfSubSeries_p.$append")
end
replace_all("@titleOfSubSeries_p.*", "\\s?\\.?\\s?$", "")
replace_all("@titleOfSubSeries_p.*", "<", "")
replace_all("@titleOfSubSeries_p.*", ">", "")
prepend("@titleOfSubSeries_p.1", ": ")

paste("@titleOfSubSeries", "@titleOfSubSeries_n",  "@titleOfSubSeries_p.1", join_char: "")

copy_field("@titleOfSubSeries", "titleOfSubSeries")
vacuum()
prepend("@titleOfSubSeries",", ")

paste("title","@title", "@titleOfSubSeries", join_char: "")

# 246 - Varying Form of Title (R) - Subfields: $a (NR)

set_array("alternativeTitle[]")
copy_field("246?[ 345678].a","alternativeTitle[].$append")

set_array("otherTitleInformation[]")
copy_field("245??.b","otherTitleInformation[].$append")
replace_all("otherTitleInformation[].*","\\s?[./]\\s?$","")

# 250 - Edition Statement (R) - Subfields: $a (NR)

set_array("edition[]")
copy_field("250  .a","edition[].$append")
replace_all("edition[].*","\\s?[./]\\s?$","")

# 260 - Publication, Distribution, etc. (Imprint) (R) - Subfield: $a (R), $b (R), $c (R)
# 264 - Production, Publication, Distribution, Manufacture, and Copyright Notice (R) -  Subfield: $a (R), $b (R), $c  (R)
# 008,18 for frequency
# 515 - Numbering Peculiarities Note (R)

set_array("publication[]")
do list(path:"260[ 23] |264[ 23][ 1]", "var":"$i")
  add_field("publication[].$append.test","")
  paste("publication[].$last.publicationHistory", "$i.c")
  do list(path: "$i.c", "var":"$j")
    replace_all("$j", "\\[|\\]|ca. |c ", "")
    unless exists("publication[].$last.startDate")
      if any_match("$j",".*?([01]\\d{3}|20\\d{2}).*")
        paste("publication[].$last.startDate", "$j")
      end
    end
    unless exists("publication[].$last.endDate")
      if any_match("$j",".*-([01]\\d{3}|20\\d{2})$")
        paste("publication[].$last.endDate", "$j")
      end
    end
  end
  # TODO is there a way to distinguish PublicationEvent and SecondaryPublicationEvent?
  set_array("publication[].$last.type[]","PublicationEvent")
  set_array("publication[].$last.location[]")
  copy_field("$i.a", "publication[].$last.location[].$append")
  set_array("publication[].$last.publishedBy[]")
  copy_field("$i.b", "publication[].$last.publishedBy[].$append")
  set_array("publication[].$last.frequency[]")
  if any_match("leader","^.{6}(a[bis]|m[bis]).*$") # checks if continous ressource
    unless any_match("008","^.{18}[#\\| u].*$") # filters out not matching values and also the value unknown
      copy_field("008","publication[].$last.frequency[].$append.id")
      replace_all("publication[].$last.frequency[].$last.id", "^.{18}(.).*$", "http://marc21rdf.info/terms/continuingfre#$1")
    end
  elsif any_match("006","^s.*$")
    do list(path: "006", "var":"$z")
      if any_match("$z","^s.*$")
        unless any_match("$z","^.[#\\| u].*$")
          copy_field("$z","publication[].$last.frequency[].$append.id")
          replace_all("publication[].$last.frequency[].$last.id", "^.(.).*$", "http://marc21rdf.info/terms/continuingfre#$1")
        end
      end
    end
  end
  set_array("publication[].$last.note[]")
  copy_field("515??.a","publication[].$last.note[].$append")
end

do list(path: "publication[]", "var": "$i")
  replace_all("$i.startDate", ".*?([01]\\d{3}|20\\d{2}).*", "$1")
  replace_all("$i.endDate", ".*-([01]\\d{3}|20\\d{2})$", "$1")
  replace_all("$i.location[].*", "^\\[|\\]$", "")
  replace_all("$i.publishedBy[].*", "^[©]|\\s?[,:;/=]?$", "")
  uniq("$i.location[]")
end



# 246 - Varying Form of Title (R) - $a - Title proper/short title (NR)

set_array("titleKeyword[]")
do list(path: "24610", "var": "$i")
  copy_field("$i.a","titleKeyword[].$append")
end


# 210 - Abbreviated Title (R) - $a - Abbreviated title (NR)
set_array("shortTitle[]")
do list(path: "210??", "var": "$i")
  copy_field("$i.a","shortTitle[].$append")
end
